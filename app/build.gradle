plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "kotlin-kapt"
    id "dagger.hilt.android.plugin"
    id "com.google.devtools.ksp" version "1.6.10-1.0.4"
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "tech.developingdeveloper.printme"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    applicationVariants.all { variant ->
        kotlin.sourceSets {
            getByName(variant.name) {
                kotlin.srcDir("build/generated/ksp/${variant.name}/kotlin")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += [
                "-Xopt-in=kotlin.RequiresOptIn",
                "-Xopt-in=androidx.compose.material.ExperimentalMaterialApi",
                "-Xopt-in=androidx.compose.foundation.ExperimentalFoundationApi"
        ]
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
}

dependencies {

    implementation libs.activity.compose
    implementation libs.androidx.core.ktx
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.compose.constraint.layout
    implementation libs.compose.ui
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"

    implementation libs.compose.material
    implementation libs.compose.material.icons.extended

    implementation libs.compose.hilt.navigation
    implementation libs.hilt
    kapt libs.hilt.kapt

    implementation libs.logging.intercepotor
    implementation libs.moshi
    implementation libs.moshi.kotlin
    implementation libs.retrofit
    implementation libs.retrofit.moshi.convertor

    implementation libs.compose.destinations
    ksp libs.compose.destinations.ksp

    implementation libs.common.io

    testImplementation libs.junit

    androidTestImplementation libs.androidx.test.junit
    androidTestImplementation libs.androidx.test.espresso.core
    androidTestImplementation libs.compose.ui.test.junit

    debugImplementation libs.compose.ui.test.manifest
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"

    coreLibraryDesugaring libs.desugaring
}